void
show_list(list l)
{
    integer i;
    text s;

    o_text("[");

    s = "";

    i = 0;
    while (i < l_length(l)) {
        o_text(s);
        if (l_s_integer(l, i)) {
            o_integer(l_q_integer(l, i));
        } else {
            show_list(l_q_list(l, i));
        }
        s = ", ";
        i += 1;
    }

    o_text("]");
}

void
flat(list c, list l)
{
    integer i;

    i = 0;
    while (i < l_length(l)) {
        if (l_s_integer(l, i)) {
            lb_p_integer(c, l_q_integer(l, i));
        } else {
            flat(c, l_q_list(l, i));
        }
        i += 1;
    }
}

list
flatten(list l)
{
    list c;

    flat(c, l);

    return c;
}

list
nl(...)
{
    integer i;
    list l;

    i = 0;
    while (i < count()) {
        l_append(l, $i);
        i += 1;
    }

    return l;
}

integer
main(void)
{
    list l;

    l_set(l, nl(nl(1), 2, nl(nl(3, 4), 5), nl(nl(nl())), nl(nl(nl(6))), 7, 8,
          nl()));

    show_list(l);
    o_byte('\n');

    show_list(flatten(l));
    o_byte('\n');

    return 0;
}

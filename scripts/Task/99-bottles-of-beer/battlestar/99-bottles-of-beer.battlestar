const bottle = " bottle"
const plural = "s"
const ofbeer = " of beer"
const wall = " on the wall"
const sep = ", "
const takedown = "Take one down and pass it around, "
const u_no = "No"
const l_no = "no"
const more = " more bottles of beer"
const store = "Go to the store and buy some more, "
const dotnl = ".\n"
const nl = "\n"

// Print two digits, use the value in a
fun printnum
    b = a
    loop
        break (a < 10)
        a /= 10
        // modulo is in the d register after idiv
        b = d
        a += 48 // ASCII value for '0'
        print(chr(a))
        break
    end
    a = b
    a += 48 // ASCII value for '0'
    print(chr(a))
end

fun main
    loop 99
        // Save loop counter for later, twice
        c -> stack
        c -> stack

        // Print the loop counter (passed in the a register)
        a = c
        printnum()

        // N, "bottles of beer on the wall, "
        print(bottle)
        print(plural)
        print(ofbeer)
        print(wall)
        print(sep)

        // Retrieve and print the number
        stack -> a
        printnum()

        // N, "bottles of beer."
        print(bottle)
        print(plural)
        print(ofbeer)
        print(dotnl)

        // "Take one down and pass it around,"
        print(takedown)

        // N-1, "bottles of beer on the wall."
        stack -> a
        a--
        // Store N-1, used just a few lines down
        a -> stack
        printnum()
        print(bottle)
        // Retrieve N-1
        stack -> a
        // Write an "s" if the count is not 1
        a != 1
            print(plural)
        end
        // Write the rest
        print(ofbeer)
        print(wall)
        print(dotnl)

        // Blank line
        print(nl)

        // Skip to the top of the loop while the counter is >= 2
        continue (c >= 2)

        // At the last two

        // "1 bottle of beer on the wall,"
        a = 1
        printnum()
        print(bottle)
        print(ofbeer)
        print(wall)
        print(sep)

        // "1 bottle of beer."
        a = 1
        printnum()
        print(bottle)
        print(ofbeer)
        print(dotnl)

        // "Take one down and pass it around,"
        print(takedown)

        // "no more bottles of beer on the wall."
        print(l_no)
        print(more)
        print(wall)
        print(dotnl)

        // Blank line
        print(nl)

        // "No more bottles of beer on the wall,"
        print(u_no)
        print(more)
        print(wall)
        print(sep)

        // "no more bottles of beer."
        print(l_no)
        print(more)
        print(dotnl)

        // "Go to the store and buy some more,"
        print(store)

        // "99 bottles of beer on the wall."
        a = 99
        printnum()
        print(bottle)
        print(plural)
        print(ofbeer)
        print(wall)
        print(dotnl)
    end
end

// vim: set syntax=c ts=4 sw=4 et:

constant seasons = {"Chaos", "Discord", "Confusion", "Bureaucracy", "The Aftermath"}
constant weekday = {"Sweetmorn", "Boomtime", "Pungenday", "Prickle-Prickle", "Setting Orange"}
constant apostle = {"Mungday", "Mojoday", "Syaday", "Zaraday", "Maladay"}
constant holiday = {"Chaoflux", "Discoflux", "Confuflux", "Bureflux", "Afflux"}

function discordianDate(sequence dt)
string dyear, dseas, dwday
integer  leap, doy, dsday,dseason
integer {y,m,d} = dt
    dyear = sprintf("%d",y+1166)
    leap = isleapyear(y)
    if leap and m=2 and d=29 then
        return "St. Tib's Day, in the YOLD " & dyear
    end if

    doy = day_of_year(y,m,d)-1
    if leap and doy>=60 then
        doy -= 1
    end if

    dsday = remainder(doy,73)+1
    dseason = floor(doy/73+1)
    if dsday=5 then
        return apostle[dseason] & ", in the YOLD " & dyear
    elsif dsday=50 then
        return holiday[dseason] & ", in the YOLD " & dyear
    end if

    dseas = seasons[dseason]
    dwday = weekday[remainder(doy,5)+1]

    return sprintf("%s, day %d of %s in the YOLD %s", {dwday, dsday, dseas, dyear})
end function

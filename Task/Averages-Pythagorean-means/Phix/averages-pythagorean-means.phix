function arithmetic_mean(sequence s)
    return sum(s)/length(s)
end function

function geometric_mean(sequence s)
atom p = 1
    for i=1 to length(s) do
        p *= s[i]
    end for
    return power(p,1/length(s))
end function

function harmonic_mean(sequence s)
atom rsum = 0
    for i=1 to length(s) do
        rsum += 1/s[i]
    end for
    return length(s)/rsum
end function

function iff(integer condition, object Tval, object Fval)
    if condition then return Tval else return Fval end if
end function

constant s = {1,2,3,4,5,6,7,8,9,10}
constant arithmetic = arithmetic_mean(s),
         geometric = geometric_mean(s),
         harmonic = harmonic_mean(s)
printf(1,"Arithmetic: %.10g\n", arithmetic)
printf(1,"Geometric: %.10g\n", geometric)
printf(1,"Harmonic: %.10g\n", harmonic)
printf(1,"Arithmetic>=Geometric>=Harmonic: %s\n", {iff((arithmetic>=geometric and geometric>=harmonic),"true","false")})

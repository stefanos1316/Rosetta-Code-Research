text
sum(text a, text b)
{
    data d;
    integer e, f, n, r;

    e = length(a);
    f = length(b);

    r = 0;

    n = min(e, f);
    while (n) {
        n -= 1;
        e -= 1;
        f -= 1;
        r += a[e] - '0';
        r += b[f] - '0';
        b_insert(d, 0, r % 10 + '0');
        r /= 10;
    }

    if (f) {
        e = f;
        a = b;
    }

    while (e) {
        e -= 1;
        r += a[e] - '0';
        b_insert(d, 0, r % 10 + '0');
        r /= 10;
    }

    if (r) {
        b_insert(d, 0, r + '0');
    }

    return b_string(d);
}

text
fibs(list l, integer n)
{
    integer c, i;
    text a, b, w;

    l_r_integer(l, 1, 1);

    a = "0";
    b = "1";
    i = 1;
    while (i < n) {
        w = sum(a, b);
        a = b;
        b = w;
        c = w[0] - '0';
        l_r_integer(l, c, 1 + l_q_integer(l, c));
        i += 1;
    }

    return w;
}

integer
main(void)
{
    integer i, n;
    list f;
    real m;

    n = 1000;

    i = 10;
    while (i) {
        i -= 1;
        lb_p_integer(f, 0);
    }

    fibs(f, n);

    m = 100r / n;

    o_text("\t\texpected\t   found\n");
    i = 0;
    while (i < 9) {
        i += 1;
        o_winteger(8, i);
        o_wpreal(16, 3, 3, 100 * log10(1 + 1r / i));
        o_wpreal(16, 3, 3, l_q_integer(f, i) * m);
        o_text("\n");
    }

    return 0;
}

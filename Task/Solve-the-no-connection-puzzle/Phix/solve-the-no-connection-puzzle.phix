constant txt = """
        A   B
       /|\ /|\
      / | X | \
     /  |/ \|  \
    C - D - E - F
     \  |\ /|  /
      \ | X | /
       \|/ \|/
        G   H"""
--constant links = "CA DA DB DC EA EB ED FB FE GC GD GE HD HE HF"
constant links = {"","","A","ABC","ABD","BE","CDE","DEF"}

function solve(sequence s, integer idx, sequence part)
object res
integer v, p
    for i=1 to length(s) do
        v = s[i]
        for j=1 to length(links[idx]) do
            p = links[idx][j]-'@'
            if abs(v-part[p])<2 then v=0 exit end if
        end for
        if v then
            if length(s)=1 then return part&v end if
            res = solve(s[1..i-1]&s[i+1..$],idx+1,part&v)
            if sequence(res) then return res end if
        end if
    end for
    return 0
end function

printf(1,substitute_all(txt,"ABCDEFGH",solve("12345678",1,"")))

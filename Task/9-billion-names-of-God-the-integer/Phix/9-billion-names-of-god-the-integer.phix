--
--  Phix does not have a bignum library, and I have not attempted any formatting.
--  sum(1234) shows 1.5697879723e+35, not 156978797223733228787865722354959930,
--  and I did not wait to see if sum(12345) would finish. [for sum read cumu[$]]
--  If you want to try plotting things, then demo\arwendemo\demo_curve_fit.exw
--  might get you started.

function min(atom a, atom b)
    if a<=b then return a end if
    return b
end function

sequence cache = {{1}}
function cumu(integer n)
sequence r
    for l=length(cache) to n do
        r = {0}
        for x=1 to l do
            r = append(r,r[-1]+cache[l-x+1][min(x,l-x)+1])
        end for
        cache = append(cache,r)
    end for
    return cache[n]
end function

function row(integer n)
sequence r = cumu(n+1)
sequence res = repeat(0,n)
    for i=1 to n do
        res[i] = r[i+1]-r[i]
    end for
    return res
end function

constant cx = {23, 123, 1234} --, 12345}
procedure nine_billion_names()

    puts(1,"rows:\n")
    for x=1 to 25 do
        printf(1,"%2d:",x)
        ?row(x)
    end for

    puts(1,"sums:\n")
    for i=1 to length(cx) do
        printf(1,"%2d:",cx[i])
        ?cumu(cx[i]+1)[$]
    end for
    if getc(0) then end if
end procedure

    nine_billion_names()

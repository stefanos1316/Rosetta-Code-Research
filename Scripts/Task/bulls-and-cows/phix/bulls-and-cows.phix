constant N = 4

function mask(integer ch)
    return power(2,ch-'1')
end function

function score(sequence guess, sequence goal)
integer bits = 0, bulls = 0, cows = 0, b
    for i=1 to N do
        b = goal[i]
        if guess[i]=b then
            bulls += 1
        else
            bits += mask(b)
        end if
    end for
    for i=1 to N do
        b = mask(guess[i])
        if and_bits(bits,b)!=0 then
            cows += 1
            bits -= b
        end if
    end for
    return {bulls, cows}
end function

procedure game()
sequence tgt = shuffle("123456789")[1..N]
integer attempt = 1, bulls = 0, cows
sequence guess
    while bulls<N do
        while 1 do
            printf(1,"Enter a %d digit number using only the digits 1 to 9:",N)
            guess = trim(gets(0))
            if length(guess)=N then exit end if
            if length(guess)=1 and lower(guess)="q" then
                puts(1,"\nthe secret number was:"&tgt)
                {} = wait_key()
                abort(0)
            end if
            printf(1," - length is not %d  (enter q to give up)\n",N)
        end while
        {bulls, cows} = score(guess,tgt)
        printf(1,"   Guess %-2d  (%s)  bulls:%d  cows:%d\n",{attempt,guess,bulls,cows})
        attempt += 1
    end while
    puts(1,"Well done!\n")
end procedure

game()

function heroArea(integer a, b, c)
atom s = (a+b+c)/2
    return sqrt(s*(s-a)*(s-b)*(s-c))
end function

function hero(atom h)
    return remainder(h,1)=0 and h>0
end function

sequence list = {}
integer tries = 0
    for a=1 to 200 do
        for b=1 to a do
            for c=1 to b do
                tries += 1
                if gcd({a,b,c})=1 then
                    atom hArea = heroArea(a,b,c)
                    if hero(hArea) then
                        list = append(list,{hArea,a+b+c,a,b,c})
                    end if
                end if
            end for
        end for
    end for
    list = sort(list)
    printf(1,"Primitive Heronian triangles with sides up to 200: %d (of %,d tested)\n\n",{length(list),tries})
    printf(1,"First 10 ordered by area/perimeter/sides:\n")
    printf(1,"area  perimeter sides")
    for i=1 to 10 do
        printf(1,"\n%4d     %3d    %dx%dx%d",list[i])
    end for
    printf(1,"\n\narea = 210:\n")
    printf(1,"area  perimeter sides")
    for i=1 to length(list) do
        if list[i][1]=210 then
            printf(1,"\n%4d     %3d    %dx%dx%d",list[i])
        end if
    end for

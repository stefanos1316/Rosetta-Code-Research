text
reverse(text s)
{
    data b;
    integer i;

    i = length(s);
    while (i) {
        i -= 1;
        b_insert(b, -1, s[i]);
    }

    return b_string(b);
}

integer
main(void)
{
    integer c, p;
    record r;
    file f;
    text s;

    f_affix(f, "unixdict.txt");

    while (f_line(f, s) != -1) {
        r_p_integer(r, s, 0);
    }

    c = 0;
    p = 0;

    if (r_first(r, s)) {
        do {
            text t;

            t = reverse(s);
            if (compare(s, t) > 0) {
                if (r_key(r, t)) {
                    p += 1;
                    if (c < 5) {
                        c += 1;
                        o_text(s);
                        o_byte(' ');
                        o_text(t);
                        o_byte('\n');
                    }
                }
            }
        } while (r_greater(r, s, s));
    }

    o_form("Semordnilap pairs: ~\n", p);

    return 0;
}

void
Add_Employee(record employees, text name, text id, integer salary,
	     text department)
{
    l_bill(r_n_list(employees, name), -1, name, id, salary, department);
}

void
collect(record top, record employees)
{
    text name;

    r_first(employees, name);
    do {
	list department, l;

	l_set(l, r_q_list(employees, name));

	if (r_key(top, l_q_text(l, 3))) {
	    integer i, salary;

	    l_set(department, r_q_list(top, l_q_text(l, 3)));

	    salary = l_q_integer(l, 2);
	    i = 0;
	    while (i < l_length(department)) {
		if (l_q_integer(l_q_list(department, i), 2) < salary) {
		    break;
		}
		i += 1;
	    }

	    l_l_list(department, i, l);

	    if (l_length(department) == N + 1) {
		l_delete(department, N);
	    }
	} else {
	    l_set(department, r_n_list(top, l_q_text(l, 3)));
	    l_l_list(department, 0, l);
	}
    } while (r_greater(employees, name, name));
}

void
print(record top)
{
    text department;

    r_first(top, department);
    do {
	list employees;
	integer i;

	o_plan("Department ", department, "\n");

	l_set(employees, r_q_list(top, department));
	i = 0;
	while (i < l_length(employees)) {
	    list l;

	    l_set(l, l_q_list(employees, i));

	    o_form("  ~ | ~ | ~\n", l_q_text(l, 0), l_q_text(l, 1),
		   l_q_integer(l, 2));

	    i += 1;
	}
    } while (r_greater(top, department, department));
}

integer
main(void)
{
    record employees, top;

    Add_Employee(employees, "Tyler Bennett  ", "E10297", 32000, "D101");
    Add_Employee(employees, "John Rappl     ", "E21437", 47000, "D050");
    Add_Employee(employees, "George Woltman ", "E00127", 53500, "D101");
    Add_Employee(employees, "Adam Smith     ", "E63535", 18000, "D202");
    Add_Employee(employees, "Claire Buckman ", "E39876", 27800, "D202");
    Add_Employee(employees, "David McClellan", "E04242", 41500, "D101");
    Add_Employee(employees, "Rich Holcomb   ", "E01234", 49500, "D202");
    Add_Employee(employees, "Nathan Adams   ", "E41298", 21900, "D050");
    Add_Employee(employees, "Richard Potter ", "E43128", 15900, "D101");
    Add_Employee(employees, "David Motsinger", "E27002", 19250, "D202");
    Add_Employee(employees, "Tim Sampair    ", "E03033", 27000, "D101");
    Add_Employee(employees, "Kim Arlich     ", "E10001", 57000, "D190");
    Add_Employee(employees, "Timothy Grove  ", "E16398", 29900, "D190");

    collect(top, employees);

    print(top);

    return 0;
}
